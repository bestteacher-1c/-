Процедура ЗаполнитьПодразделениеСтороныПроводки(Движение, Подразделение,
		ДтКт) Экспорт

	ПараметрыФО = Новый Структура;
	ПараметрыФО.Вставить("ПФО_Организация", Движение.Организация);

	ПоПодразделениям = ПолучитьФункциональнуюОпцию("УчетПоПодразделениям", ПараметрыФО);

	Если ПоПодразделениям И Движение["Счет" + ДтКт].ПоПодразделениям Тогда

		Движение["Подразделение" + ДтКт] = Подразделение;

	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьСубконтоСтороныПроводки(Движение, ИмяИлиКоличествоСубконто,
		ЗначениеСубконто, ДтКт) Экспорт

	Если (ЗначениеЗаполнено(Движение["Счет" + ДтКт]) = Ложь) Тогда
		Возврат; //Если не заполнен счет, то субконто не надо заполнять! 
	КонецЕсли;

	Если ТипЗнч(ИмяИлиКоличествоСубконто) = Тип("Строка") Тогда

		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
			|	СчетаВидыСубконто.ВидСубконто
			|ИЗ
			|	ПланСчетов.Счета.ВидыСубконто КАК СчетаВидыСубконто
			|ГДЕ
			|	СчетаВидыСубконто.Ссылка = &СсылкаНаСчет
			|	И СчетаВидыСубконто.ВидСубконто.ИмяПредопределенныхДанных = &ИмяСубконто";

		Запрос.УстановитьПараметр("ИмяСубконто", ИмяИлиКоличествоСубконто);
		Запрос.УстановитьПараметр("СсылкаНаСчет", Движение["Счет" + ДтКт]);

		РезультатЗапроса = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			Движение["Субконто"
				+ ДтКт].Вставить(ВыборкаДетальныеЗаписи.ВидСубконто, ЗначениеСубконто);

		КонецЦикла;

	ИначеЕсли ТипЗнч(ИмяИлиКоличествоСубконто) = Тип("Число") Тогда

	//----Определяем количество субконто---
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
			|	КОЛИЧЕСТВО(СчетаВидыСубконто.ВидСубконто) КАК КоличествоСубконто
			|ИЗ
			|	ПланСчетов.Счета.ВидыСубконто КАК СчетаВидыСубконто
			|ГДЕ
			|	СчетаВидыСубконто.Ссылка = &СсылкаНаСчет";

		Запрос.УстановитьПараметр("СсылкаНаСчет", Движение["Счет" + ДтКт]);

		РезультатЗапроса = Запрос.Выполнить();

		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

		КоличествоСубконто = 0;

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			КоличествоСубконто = ВыборкаДетальныеЗаписи.КоличествоСубконто;

		КонецЦикла;

		//----Проверяем, есть ли субконто с номером у данного счета
		Если КоличествоСубконто >= ИмяИлиКоличествоСубконто Тогда

			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
				|	СчетаВидыСубконто.ВидСубконто
				|ИЗ
				|	ПланСчетов.Счета.ВидыСубконто КАК СчетаВидыСубконто
				|ГДЕ
				|	СчетаВидыСубконто.Ссылка = &Ссылка
				|	И СчетаВидыСубконто.НомерСтроки = &НомерСтроки";

			Запрос.УстановитьПараметр("НомерСтроки", ИмяИлиКоличествоСубконто);
			Запрос.УстановитьПараметр("Ссылка", Движение["Счет" + ДтКт]);

			РезультатЗапроса = Запрос.Выполнить();

			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

				Движение["Субконто"
					+ ДтКт].Вставить(ВыборкаДетальныеЗаписи.ВидСубконто, ЗначениеСубконто);

			КонецЦикла;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьКоличествоСтороныПроводки(Движение, Количество,
		ДтКт) Экспорт

	Если (Движение["Счет" + ДтКт].Количественный = Истина) Тогда

		Движение["Количество" + ДтКт] = Количество;

	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьВалютуСтороныПроводки(Движение, Валюта, СуммаВалюты,
		ДтКт) Экспорт

	Если (Движение["Счет" + ДтКт].Валютный = Истина) Тогда

		Движение["Валюта" + ДтКт] = Валюта;
		Движение["СуммаВалюты" + ДтКт] = СуммаВалюты;

	КонецЕсли;

КонецПроцедуры

// ПересчитатьИзВалютыВВалюту()

// Процедура - Изменить активность движений
//
// Параметры:
//  Ссылка			 - ДокументСсылка	-  Может быть ссылка на любой документ
//  НужнаяАктивность - Булево	 -  По Умолчанию = Неопределено
//
Процедура ИзменитьАктивностьДвижений(Ссылка,
		НужнаяАктивность = Неопределено) Экспорт

	НЗ = РегистрыБухгалтерии.Проводки.СоздатьНаборЗаписей();
	НЗ.Отбор.Регистратор.Установить(Ссылка);

	НЗ.Прочитать();

	Если НужнаяАктивность = Неопределено Тогда

		Если НЗ.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;

		НужнаяАктивность = Не НЗ[0].Активность;

	КонецЕсли;

	НЗ.УстановитьАктивность(НужнаяАктивность);

	НЗ.Записать();

КонецПроцедуры

// ИзменитьАктивностьДвижений()
Процедура ПроверкаЗаполненияПодразделенияОбработкаПроверкиЗаполнения(Источник,
		Отказ, ПроверяемыеРеквизиты) Экспорт

	ПараметрыФО = Новый Структура;
	ПараметрыФО.Вставить("ПФО_Организация", Источник.Организация);

	ПоПодразделениям = ПолучитьФункциональнуюОпцию("УчетПоПодразделениям", ПараметрыФО);

	Если ПоПодразделениям = Ложь Тогда

		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Подразделение"));

	КонецЕсли;

КонецПроцедуры

Процедура СформироватьПроводкиКурсовыеРазницы(Движение, НаборЗаписей,
		ДтКт) Экспорт

	Если Движение["Счет" + ДтКт].Валютный Тогда

		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
			|	ПроводкиОстатки.СуммаОстатокДт,
			|	ПроводкиОстатки.СуммаОстатокКт,
			|	ПроводкиОстатки.СуммаВалютыОстатокДт,
			|	ПроводкиОстатки.СуммаВалютыОстатокКт
			|ИЗ
			|	РегистрБухгалтерии.Проводки.Остатки(
			|			&Период,
			|			Счет = &Счет,
			|			,
			|			Организация = &Организация
			|				И Валюта = &Валюта) КАК ПроводкиОстатки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КурсыВалютСрезПоследних.Курс
			|ИЗ
			|	РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &Валюта) КАК КурсыВалютСрезПоследних";

		Запрос.УстановитьПараметр("Валюта", Движение["Валюта" + ДтКт]);
		Запрос.УстановитьПараметр("Организация", Движение.Организация);
		Запрос.УстановитьПараметр("Период", Движение.Период);
		Запрос.УстановитьПараметр("Счет", Движение["Счет" + ДтКт]);

		СхемаЗапроса = Новый СхемаЗапроса;

		СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);

		Если Движение["Счет" + ДтКт].ПоПодразделениям Тогда

			Таблица_Из = СхемаЗапроса.ПакетЗапросов.Получить(0).Операторы.Получить(0).Источники.Получить(0);

			УсловиеНаИзмерение = Строка(Таблица_Из.Источник.Параметры.Получить(3).Выражение);

			Таблица_Из.Источник.Параметры.Получить(3).Выражение = Новый ВыражениеСхемыЗапроса(""
				+ УсловиеНаИзмерение + " И Подразделение = &Подразделение");

			Запрос.УстановитьПараметр("Подразделение", Движение["Подразделение"
				+ ДтКт]);

		КонецЕсли;

		Для каждого ТекСубконто Из Движение["Субконто" + ДтКт] Цикл

			СтрокаТЧВидыСубконто = Движение["Счет"
				+ ДтКт].ВидыСубконто.Найти(ТекСубконто.Ключ, "ВидСубконто");

			Если СтрокаТЧВидыСубконто.ТолькоОбороты Тогда
				Продолжить;
			КонецЕсли;

			НомерСубконто = СтрокаТЧВидыСубконто.НомерСтроки;

			Таблица_Из = СхемаЗапроса.ПакетЗапросов.Получить(0).Операторы.Получить(0).Источники.Получить(0);

			УсловиеНаИзмерение = Строка(Таблица_Из.Источник.Параметры.Получить(3).Выражение);

			Таблица_Из.Источник.Параметры.Получить(3).Выражение = Новый ВыражениеСхемыЗапроса(""
				+ УсловиеНаИзмерение + " И Субконто" + НомерСубконто + " = &Субконто"
				+ НомерСубконто);

			Запрос.УстановитьПараметр("Субконто"
				+ НомерСубконто, ТекСубконто.Значение);

		КонецЦикла;

		Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();

		МассивРезультатов = Запрос.ВыполнитьПакет();

		РезультатЗапроса = МассивРезультатов[1];

		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			Курс = ВыборкаДетальныеЗаписи.Курс;

		КонецЦикла;

		РезультатЗапроса = МассивРезультатов[0];

		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

		ТекущаяОценкаДт = 0;
		ТекущаяОценкаКт = 0;

		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл

			ТекущаяОценкаДт = ВыборкаДетальныеЗаписи.СуммаВалютыОстатокДт * Курс;

			Если ТекущаяОценкаДт = 0 Тогда

				ТекущаяОценкаКт = ВыборкаДетальныеЗаписи.СуммаВалютыОстатокКт * Курс;

				КурсоваяРазница = ВыборкаДетальныеЗаписи.СуммаОстатокКт
					- ВыборкаДетальныеЗаписи.СуммаОстатокДт - ТекущаяОценкаКт;

			Иначе

				КурсоваяРазница = ВыборкаДетальныеЗаписи.СуммаОстатокДт
					- ВыборкаДетальныеЗаписи.СуммаОстатокКт - ТекущаяОценкаДт;

			КонецЕсли;

			Если КурсоваяРазница = 0 Тогда

				Продолжить;

			ИначеЕсли ТекущаяОценкаДт > 0 и КурсоваяРазница > 0 Или ТекущаяОценкаКт > 0
					И КурсоваяРазница < 0 Тогда

				Проводка = НаборЗаписей.Добавить();

				Проводка.СчетДт = ПланыСчетов.Счета.РасходыДоходы;

				Проводка.СчетКт = Движение["Счет" + ДтКт];

				Для каждого ТекСубконто Из Движение["Субконто" + ДтКт] Цикл

					Проводка.СубконтоКт.Вставить(ТекСубконто.Ключ, ТекСубконто.Значение);

				КонецЦикла;

				Серверный.ЗаполнитьПодразделениеСтороныПроводки(Проводка, Движение["Подразделение"
					+ ДтКт], "Кт");

				Проводка.Период = Движение.Период;
				Проводка.Организация = Движение.Организация;
				Проводка.Сумма = ?(КурсоваяРазница > 0, КурсоваяРазница, -КурсоваяРазница);
				Проводка.СодержаниеПроводки = "Курсовая разница";

				//ИначеЕсли ТекущаяОценкаДт < 0 и КурсоваяРазница < 0 Или ТекущаяОценкаКт > 0 И КурсоваяРазница > 0 Тогда
			Иначе
				Проводка = НаборЗаписей.Добавить();

				Проводка.СчетКт = ПланыСчетов.Счета.РасходыДоходы;

				Проводка.СчетДт = Движение["Счет" + ДтКт];

				Для каждого ТекСубконто Из Движение["Субконто" + ДтКт] Цикл

					Проводка.СубконтоДт.Вставить(ТекСубконто.Ключ, ТекСубконто.Значение);

				КонецЦикла;

				Серверный.ЗаполнитьПодразделениеСтороныПроводки(Проводка, Движение["Подразделение"
					+ ДтКт], "Дт");

				Проводка.Период = Движение.Период;
				Проводка.Организация = Движение.Организация;
				Проводка.Сумма = ?(КурсоваяРазница > 0, КурсоваяРазница, -КурсоваяРазница);
				Проводка.СодержаниеПроводки = "Курсовая разница";

			КонецЕсли;

		КонецЦикла;

	КонецЕсли;

КонецПроцедуры
