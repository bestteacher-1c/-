Функция ПолучитьСвойствоСчета(Счет) Экспорт

	ДанныеСчета = Новый Структура;
	
	МаксКоличествоСубконто = Метаданные.ПланыСчетов.Счета.МаксКоличествоСубконто;

	ДанныеСчета.Вставить("МаксКоличествоСубконто", МаксКоличествоСубконто);


	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	СчетаВидыСубконто.Ссылка.Забалансовый,
		|	СчетаВидыСубконто.Ссылка.ПоПодразделениям,
		|	СчетаВидыСубконто.Ссылка.Количественный,
		|	СчетаВидыСубконто.Ссылка КАК Ссылка,
		|	СчетаВидыСубконто.ВидСубконто КАК ВидСубконто,
		|	СчетаВидыСубконто.ВидСубконто.Наименование,
		|	СчетаВидыСубконто.ВидСубконто.ИмяПредопределенныхДанных,
		|	СчетаВидыСубконто.ВидСубконто.ТипЗначения
		|ИЗ
		|	ПланСчетов.Счета.ВидыСубконто КАК СчетаВидыСубконто
		|ГДЕ
		|	СчетаВидыСубконто.Ссылка = &Ссылка
		|ИТОГИ
		|	КОЛИЧЕСТВО(ВидСубконто)
		|ПО
		|	Ссылка";

	Запрос.УстановитьПараметр("Ссылка", Счет);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаСсылка.Следующий() Цикл

		ДанныеСчета.Вставить("Забалансовый", ВыборкаСсылка.Забалансовый);
		ДанныеСчета.Вставить("ПоПодразделениям", ВыборкаСсылка.ПоПодразделениям);
		ДанныеСчета.Вставить("Количественный", ВыборкаСсылка.Количественный);
		ДанныеСчета.Вставить("КоличествоСубконто", ВыборкаСсылка.ВидСубконто);


		ВыборкаДетальныеЗаписи = ВыборкаСсылка.Выбрать();

		Для Индекс = 1 По МаксКоличествоСубконто Цикл

			Если ВыборкаДетальныеЗаписи.Следующий() = Истина Тогда

				ДанныеСчета.Вставить("ВидСубконто"
					+ Индекс, ВыборкаДетальныеЗаписи.ВидСубконто);
				ДанныеСчета.Вставить("ВидСубконто" + Индекс
					+ "Наименование", ВыборкаДетальныеЗаписи.Наименование);
				ДанныеСчета.Вставить("ВидСубконто" + Индекс
					+ "ИмяПредопределенныхДанных", ВыборкаДетальныеЗаписи.ИмяПредопределенныхДанных);
				ДанныеСчета.Вставить("ВидСубконто" + Индекс
					+ "ТипЗначения", ВыборкаДетальныеЗаписи.ТипЗначения);

			Иначе

				ДанныеСчета.Вставить("ВидСубконто" + Индекс, Неопределено);
				ДанныеСчета.Вставить("ВидСубконто" + Индекс
					+ "Наименование", Неопределено);
				ДанныеСчета.Вставить("ВидСубконто" + Индекс
					+ "ИмяПредопределенныхДанных", Неопределено);
				ДанныеСчета.Вставить("ВидСубконто" + Индекс
					+ "ТипЗначения", Неопределено);

			КонецЕсли;

		КонецЦикла;

	КонецЦикла;



	Возврат ДанныеСчета;

КонецФункции

// ()
